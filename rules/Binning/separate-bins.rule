rule binning_separate_bins:
    input:
        expand("Binning/contigs2clusters.{pk}.{nn}.tsv",pk=config["binning"]["pk"],nn=config["binning"]["nn"]),
        'Assembly/%s.assembly.merged.fa' % ASS,
        "Binning/ORFS-contig_links.bed",
        "Analysis/annotation/prokka.faa",
        "Analysis/annotation/annotation.filt.gff"
    output:
        dynamic("Binning/clusters/{cluster}.clt")
    shell:
        """
        mkdir -p Binning/clusters
        for cluster in $(tail -n+2 {input[0]} | cut -f2 | sort | uniq)
        do
            # Create directory for the cluster
            mkdir -p Binning/clusterFiles/${{cluster}}

            # Define headerfiles
            headerfile="Binning/clusterFiles/${{cluster}}/cluster.${{cluster}}.contigids"
            protids="Binning/clusterFiles/${{cluster}}/cluster.${{cluster}}.protids"

            echo "Obtaining contigs for cluster ${{cluster}}"
            awk -v c=${{cluster}} '{{ if($2==c) {{print $1}}  }}' {input[0]} > ${{headerfile}}
            pullseq -i {input[1]} -n ${{headerfile}}  > Binning/clusterFiles/${{cluster}}/cluster.${{cluster}}.fa

            echo "Obtaining amino acid sequences for cluster ${{cluster}}"
            awk 'FNR==NR{{a[$1]=$1;next}}{{if(a[$1]) print $4}}' ${{headerfile}} {input[2]} > ${{protids}}
            pullseq -i {input[3]} -n ${{protids}}  > Binning/clusterFiles/${{cluster}}/cluster.${{cluster}}.faa

            echo "Obtaining functional annotations (gff format) for cluster/bin ${{cluster}}"
            grep -wFf ${{headerfile}} {input[4]} > Binning/clusterFiles/${{cluster}}/cluster.${{cluster}}.gff
            touch Binning/clusters/${{cluster}}.clt
        done
        echo "Complete separating bins"
        """
