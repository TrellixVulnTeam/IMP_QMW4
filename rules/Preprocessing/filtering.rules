FILTERING_CMD = """
        TMP_FILE=$(mktemp --tmpdir={TMPDIR} -t "alignment_XXXXXX.bam")
        BUFFER=$(mktemp --tmpdir={TMPDIR} -t "alignment_XXXXXX.bam")
        bwa mem -v 1 -t {THREADS} {input[3]} {input[0]} {input[1]} | samtools view -@ {THREADS} -bS - > $TMP_FILE
        samtools merge -@ {THREADS} -u - \
        <(samtools view -@ {THREADS} -u  -f 4 -F 264 $TMP_FILE) \
        <(samtools view -@ {THREADS} -u -f 8 -F 260 $TMP_FILE) \
        <(samtools view -@ {THREADS} -u -f 12 -F 256 $TMP_FILE) | \
        samtools view -@ {THREADS} -bF 0x800 -  | samtools sort -o -@ {THREADS} -m {MEMCORE}G -n - $BUFFER | \
        bamToFastq -i stdin -fq {output[0]} -fq2 {output[1]}
        if [[ -s {input[2]} ]]
        then
        bwa mem -v 1 -t {THREADS} {input[3]} {input[2]} | samtools view -@ {THREADS} -bS - | \
        samtools view -@ {THREADS} -uf 4 - | bamToFastq -i stdin -fq {output[2]}
        else
        echo "{input[2]} is empty, skipping single end human sequence filtering, but creating it anyway..."
        touch {output[2]}
        fi
        rm -rf $BUFFER* $TMP_FILE
        """

SYMLINK_CMD = """
ln -fs $(echo {input[0]} | cut -f 2 -d /) {output[0]} && touch -h {output[0]}
ln -fs $(echo {input[1]} | cut -f 2 -d /) {output[1]} && touch -h {output[1]}
ln -fs $(echo {input[2]} | cut -f 2 -d /) {output[2]} && touch -h {output[2]}
"""


rule mg_filtering:
    input:
        'Preprocessing/mg.r1.trimmed.rna_filtered.fq',
        'Preprocessing/mg.r2.trimmed.rna_filtered.fq',
        'Preprocessing/mg.se.trimmed.rna_filtered.fq',
        '{dir}/{ref}.fa'.format(dir=DBPATH + "/filtering", ref=config['filtering']['filter']),
        expand(
            "{p}/filtering/{filter}.{ext}", filter=config["filtering"]["filter"],
            ext=['fa', 'fa.amb', 'fa.ann', 'fa.bwt', 'fa.pac', 'fa.sa'], p=DBPATH)
    output:
        'Preprocessing/mg.r1.{prefix}.%s_filtered.fq' % config["filtering"]["filter"],
        'Preprocessing/mg.r2.{prefix}.%s_filtered.fq' % config["filtering"]["filter"],
        'Preprocessing/mg.se.{prefix}.%s_filtered.fq' % config["filtering"]["filter"]
    shell:
        FILTERING_CMD

rule mt_filtering:
    input:
        'Preprocessing/mt.r1.trimmed.fq',
        'Preprocessing/mt.r2.trimmed.fq',
        'Preprocessing/mt.se.trimmed.fq',
        '{dir}/{ref}.fa'.format(dir=DBPATH + "/filtering", ref=config['filtering']['filter']),
        expand(
            "{p}/filtering/{filter}.{ext}", filter=config["filtering"]["filter"],
            ext=['fa', 'fa.amb', 'fa.ann', 'fa.bwt', 'fa.pac', 'fa.sa'], p=DBPATH)
    output:
        'Preprocessing/mt.r1.{prefix}.%s_filtered.fq' % config["filtering"]["filter"],
        'Preprocessing/mt.r2.{prefix}.%s_filtered.fq'% config["filtering"]["filter"] ,
        'Preprocessing/mt.se.{prefix}.%s_filtered.fq' % config["filtering"]["filter"]
    shell:
        FILTERING_CMD


rule symlink_mt_preprocessed_files:
    input:
        'Preprocessing/mt.r1.trimmed.%s_filtered.fq' % config["filtering"]["filter"],
        'Preprocessing/mt.r2.trimmed.%s_filtered.fq' % config["filtering"]["filter"],
        'Preprocessing/mt.se.trimmed.%s_filtered.fq' % config["filtering"]["filter"]
    output:
        'Preprocessing/mt.r1.preprocessed.fq',
        'Preprocessing/mt.r2.preprocessed.fq',
        'Preprocessing/mt.se.preprocessed.fq'
    shell:
        SYMLINK_CMD

rule symlink_mg_preprocessed_files:
    input:
        'Preprocessing/mg.r1.trimmed.rna_filtered.%s_filtered.fq' % config["filtering"]["filter"],
        'Preprocessing/mg.r2.trimmed.rna_filtered.%s_filtered.fq' % config["filtering"]["filter"],
        'Preprocessing/mg.se.trimmed.rna_filtered.%s_filtered.fq' % config["filtering"]["filter"]
    output:
        'Preprocessing/mg.r1.preprocessed.fq',
        'Preprocessing/mg.r2.preprocessed.fq',
        'Preprocessing/mg.se.preprocessed.fq'
    shell:
        SYMLINK_CMD
