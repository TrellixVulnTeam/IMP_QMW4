rule trimming:
    input:
        'Preprocessing/{type}.r1.fq',
        'Preprocessing/{type}.r2.fq',
        DBPATH + "/adapters/adapters.done"
    output:
        'Preprocessing/{type}.r1.trimmed.fq',
        temp('Preprocessing/{type}.se1.trimmed.fq'),
        'Preprocessing/{type}.r2.trimmed.fq',
        temp('Preprocessing/{type}.se2.trimmed.fq')
    shell:
        """
        java -jar {config[trimmomatic][jarfile]} PE -threads {THREADS} {input[0]} {input[1]} {output} \
        ILLUMINACLIP:{DBPATH}/adapters/{config[trimmomatic][adapter]}.fa:{config[trimmomatic][seed_mismatch]}:{config[trimmomatic][palindrome_clip_threshold]}:{config[trimmomatic][simple_clip_threshold]} \
        LEADING:{config[trimmomatic][leading]} \
        TRAILING:{config[trimmomatic][trailing]} \
        SLIDINGWINDOW:{config[trimmomatic][window_size]}:{config[trimmomatic][window_quality]} \
        MINLEN:{config[trimmomatic][minlen]} \
        MAXINFO:{config[trimmomatic][target_length]}:{config[trimmomatic][strictness]}
        """


rule cat_se_trimmed:
    input:
        'Preprocessing/{type}.se1.trimmed.fq',
        'Preprocessing/{type}.se2.trimmed.fq'
    output:
        'Preprocessing/{type}.se.trimmed.fq',
    shell:
        "cat {input[0]} {input[1]} > {output}"
