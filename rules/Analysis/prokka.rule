rule annotate:
    input:
        'Assembly/%s.assembly.merged.fa' % ASS,
        expand("{path}/{db}", path=DBPATH, db=config["prokka"]["databases"]),
        'Analysis/tbl2asn.updated'
    output:
        "Analysis/annotation/annotation.filt.gff",
        "Analysis/annotation/{p}.faa".format(p=config['prokka']['prefix']),
        "Analysis/annotation/{p}.fna".format(p=config['prokka']['prefix']),
        "Analysis/annotation/{p}.ffn".format(p=config['prokka']['prefix']),
        "Analysis/annotation/{p}.fsa".format(p=config['prokka']['prefix']),
    shell:
        """
        ### prokka by default will look databases where is located the binary.
        ### we have to softlink to put the binary somewhere and the databases somewhere else.
        if [[ "{DBPATH}" = /* ]]
        then
            PP={DBPATH};
        else
            PP=$PWD/{DBPATH};
        fi
        DD=$(dirname $(which prokka))/../db
        if [[ ! -L $DD ]]
        then
            CUR=$PWD
            echo "Softlinking $DD to $PP"
            cd $(dirname $(which prokka))/.. && ln -fs $PP db
            cd $CUR
        fi
        rm -rf Analysis/annotation/
        prokka --force --outdir Analysis/annotation --prefix {config[prokka][prefix]} --cpus {THREADS} --metagenome {input[0]} >> Analysis/annotation.log 2>&1

        # Prokka gives a gff file with a long header and with all the contigs at the bottom.  The command below removes the
        # And keeps only the gff table.

        LN=`grep -Hn "^>" Analysis/annotation/{config[prokka][prefix]}.gff | head -n1 | cut -f2 -d ":" || if [[ $? -eq 141 ]]; then true; else exit $?; fi`
        LN1=1
        LN=$(($LN-$LN1))
        head -n $LN Analysis/annotation/{config[prokka][prefix]}.gff | grep -v "^#" | sort | uniq | grep -v "^==" > {output[0]}
        """

rule update_tbl2asn:
    output:
        touch("Analysis/tbl2asn.updated")
    shell:
        """
        OUT=$(tbl2asn -hp /tmp 2>&1)
        echo $OUT
        if [[ "$OUT" =~ "copy of tbl2asn is more than a year old" ]]; then
                wget ftp://ftp.ncbi.nih.gov/toolbox/ncbi_tools/converters/by_program/tbl2asn/linux64.tbl2asn.gz -O /tmp/tbl2asn.gz
                gzip -f -d /tmp/tbl2asn.gz
                chmod +x /tmp/tbl2asn
                {IMP_SUDO} mv /tmp/tbl2asn /usr/bin
        fi
        """
